PREFIX dc:             <http://purl.org/dc/elements/1.1/>
PREFIX interval:       <https://rezateshnizi.com/rt-bi/tower_bridge/temporal/intervals#>
PREFIX owl:            <http://www.w3.org/2002/07/owl#>
PREFIX polygon:        <https://rezateshnizi.com/rt-bi/tower_bridge/spatial/polygons#>
PREFIX pt:             <https://rezateshnizi.com/rt-bi/tower_bridge/spatial/pts#>
PREFIX rdf:            <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:           <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rt_bi:          <https://rezateshnizi.com/rt-bi/>
PREFIX spatial_props:  <https://rezateshnizi.com/rt-bi/spatial/props#>
PREFIX t:              <https://rezateshnizi.com/rt-bi/tower_bridge/temporal/ts#>
PREFIX temporal_props: <https://rezateshnizi.com/rt-bi/temporal/props#>
PREFIX tower_bridge:   <https://rezateshnizi.com/rt-bi/tower_bridge/>
PREFIX traversability: <https://rezateshnizi.com/rt-bi/tower_bridge/traversability#>
PREFIX world_props:    <https://rezateshnizi.com/rt-bi/world/props#>
PREFIX x:              <https://rezateshnizi.com/rt-bi/tower_bridge/spatial/xs#>
PREFIX xsd:            <http://www.w3.org/2001/XMLSchema#>
PREFIX y:              <https://rezateshnizi.com/rt-bi/tower_bridge/spatial/ys#>

SELECT
	?bNode ?polygonId ?vertInd ?x ?y
	?name ?color
	?needsLegs ?needsWheels ?needsSwim
	?t1 ?t2 ?minInclusive ?maxInclusive
WHERE {
	GRAPH tower_bridge:world {
		?bNode a rt_bi:RegularSpace ;
			world_props:polygons/rdf:member ?polygonId .
		GRAPH tower_bridge:spatial {
			?polygonId spatial_props:vertices ?vertexSeq .
			?vertexSeq ?vertInd ?vertex .
			?vertex
				spatial_props:x/spatial_props:x_val ?x ;
				spatial_props:y/spatial_props:y_val ?y .
		}
		OPTIONAL { ?bNode world_props:color ?color . }
		OPTIONAL { ?bNode world_props:name ?name . }
		OPTIONAL { ?bNode world_props:traversability ?traversability .
			GRAPH tower_bridge:traversabilities {
				?traversability
					world_props:legs ?needsLegs ;
					world_props:wheels ?needsWheels ;
					world_props:swims ?needsSwim .
			}
		}
		OPTIONAL { ?bNode world_props:off_times/rdf:member ?intervalId .
			GRAPH tower_bridge:temporal {
				?intervalId
					temporal_props:min/temporal_props:secs ?t1 ;
					temporal_props:max/temporal_props:secs ?t2 ;
					temporal_props:include_min ?minInclusive ;
					temporal_props:include_max ?maxInclusive .
			}
		}
	}
}
ORDER BY ?bNode ?polygonId ?vertInd
